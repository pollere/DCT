// Trust schema for office example (ACM ICN 2021 Tutorial)
// illustrating non-fine-grained security

// Each application process in this trust domain has a role cert

// trust anchor name for this trust zone
_domain:    "office"

// Publication definition.
// _ts is set at publication build by the specified method.
// Use of mbps shim means publications have mId, sCnt (set by mbps) and
// at least one leading component to identify the domain. Between those
// components, at least one more component followed by others as needed
// This basic version doesn't put any fine-grained restrictions on who can
// say what to whom (as in office.trust) but just enforces that publications
// must be signed by valid roleCerts.

#pub: _domain/func/topic/loc/args/msgID/sCnt/_ts & { _ts: timestamp() } <= signCert

signCert: _domain/_role/_roleId/"sgn"/_keyinfo <= roleCert
roleCert: _domain/_role/_roleId/_keyinfo <= roomCert
roomCert: _domain/"room"/_roomId/_keyinfo <= configCert
configCert: _domain/"config"/_configId/_keyinfo <= domCert
domCert:    _domain/_keyinfo

// information about signing chain
#chainInfo: /_role/_roleId/_roomId <= signCert

// Publication prefix and validator types
#pubPrefix:    _domain
#msgsValidator: "EdDSA"
#certValidator: "EdDSA"
// for cAdds
#pduValidator: "EdDSA"

// The final components are KEY,keyID, issuerID and version 
_keyinfo: "KEY"/_/"dct"/_

