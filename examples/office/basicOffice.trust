// Trust schema for office example (ACM ICN 2021 Tutorial)
// illustrating non-fine-grained security

// Each application process in this trust zone has a role cert

// trust anchor name for this trust zone
_net:    "office"

// Publication definition.
// Use of mbps shim means publications have mId, sCnt, mts (set by mbps) and
// at least one leading component to identify the network. Between those
// components, at least one more component followed by others as needed
// This basic version doesn't put any fine-grained restrictions on who can
// say what to whom (as in office.trust) but just enforces that publications
// must be signed by valide roleCerts.

#pub: _net/func/topic/loc/args/msgID/sCnt/mts <= roleCert

roleCert: _net/_role/_roleId/_keyinfo <= roomCert
roomCert: _net/"room"/_roomId/_keyinfo <= configCert
configCert: _net/"config"/_configId/_keyinfo <= netCert
netCert:    _net/_keyinfo

// information about signing chain
#chainInfo: /_role/_roleId/_roomId <= roleCert

// Publication prefix and validator type
#pubPrefix:    _net
#pubValidator: "EdDSA"

// Prefix used for syncData (NDN Interest/Data)
#wirePrefix: _ndnprefix/_net & { _ndnprefix: "localnet" }
#wireValidator: "AEAD"

// The final components are KEY,keyID, issuerID and version 
_keyinfo: "KEY"/_/"dct"/_

