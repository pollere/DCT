// Home IoT Message-Based Pub/Sub 0 trust schema

// Each process has an identity associated with a particular type of
// certificate that is signed and validatable.

// trust anchor name associated with this schema
_network:    "iot0"

// MBPS Publication definition
// target, topic, topicArgs are set using application parameters
// msgID, sCnt, mts get set in mbps.hpp (the MBPS "shim") for a particular message
// In addition, all trust schemas for mbps-based applications must include a #chainInfo
// which lists all the signing chain tags so schemaLib can access them
// A particular application process can make its subscription more
// specific by using a subscription list with specific subCollections (designated
// by targets) and related callback functions.

#iotPub: _network/target/topic/trgtLoc/topicArgs/_origin/msgID/sCnt/mts & {
    _origin:   sysId()
} <= roleCert

roleCert:    _network/_role/_roleId/_keyinfo <= netCert

netCert: _network/_keyinfo

// information about signing chain
#chainInfo: /_role/_roleId <= roleCert

// schema's Publication prefix and validator type
#pubPrefix:    _network
#pubValidator: "EdDSA"

// Prefix used at the syncps (NDN Interest/Data) level to sync
// this collection. Change to AEAD for data privacy
#wirePrefix: _ndnprefix & { _ndnprefix: "localnet" }
#wireValidator: "EdDSA"

// uses NDN certificate format v2 suffix for a cert name, final four
// components are KEY, keyID, issuerID, and version
_keyinfo: "KEY"/_/"dct"/_

